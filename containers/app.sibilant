(include "../macros.sibilant")
(import-namespace react)

(var { Component PropTypes } React
     { connect } (require "react-redux")
     { sibilize display-repl } (require "../actions.sibilant")
     { Router Route Link } (require "react-router")
     { push-path } (require "redux-simple-router")
     values (require "lodash/object/values")
     *sort-by (require "lodash/collection/sortBy"))

(import-classes (require "../components/doc-listing.sibilant") DocListing)
(import-classes (require "../components/header.sibilant") Header)
(import-classes (require "../components/footer.sibilant") Footer)
(import-classes (require "../components/repl.sibilant") REPL)

(def-class App
           (var { on-filter-change tags docs filter
                  doc-names repl sibilize display-repl } this.props)
           (div
            (REPL repl: repl
                  display-repl display-repl
                  sibilize: sibilize)
            (Header on-filter-change on-filter-change,
                    display-repl display-repl
                    tags tags,
                    filter filter,
                    docs docs)
            (DocListing on-filter-change on-filter-change,
                    tags tags,
                    filter filter,
                    docs docs
                    doc-names doc-names)
            (Footer)))

(def select-docs (docs filter)
     (if (as-boolean filter)
         (|> docs
             (values)
             (.filter (#(doc)
                        (or (|> doc.tags (includes? filter))
                            (|> doc.name (match-regex? ("^" filter)))))))

         (values docs)))

(def sort-docs (docs-as-array)
     (|> docs-as-array (*sort-by (#-> (get 'tags) (includes? 'deprecated))
                                 (#-> (get 'tags) (first))
                                 'name)))

(def map-state-to-props (state props)
     (var filter (or props.route-params.filter state.filter ""))
     { docs (|> state.docs-by-name (select-docs filter) (or []) (sort-docs))
       tags (|> state.tags (or {}))
       repl state.repl
       doc-names state.doc-names
       filter filter  })


(def map-dispatch-to-props (dispatch)
     { sibilize (#-> sibilize dispatch)
       display-repl (#-> display-repl dispatch)
       on-filter-change (#> (|> (ternary #0 ("/" #0) "/") push-path dispatch)) })

(set exports 'App ((connect map-state-to-props map-dispatch-to-props)
                   App))


