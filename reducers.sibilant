(require! lodash-object "lodash/object"
          initial-state "./initial-state.sibilant"
          actions "./actions.sibilant")


(var *assign lodash-object.assign
     reducers module.exports)

(each-key attribute initial-state
          (set reducers attribute
               (#(state action)
                  (default state (get initial-state attribute)))))

(def reducers.filter (state action)
     (default state false)
     (if (= action.type actions.set-filter.name)
         (or action.filter false)
         state))

(def reducers.repl (state action)
     (default state initial-state.repl)
     (var merge-state (#> (*assign {} state #0)))
     
     (if (= action.type actions.display-js.name)
         (merge-state { compiling false
                        js-string action.js-string })

         (= action.type actions.display-repl.name)
         (merge-state { visibility action.visibility })

         (= action.type actions.display-result)
         (merge-state { result action.result})

         (and (= action.type actions.display-sibilant.name)
              (!= state.sibilant-string action.sibilant-string))
         (merge-state { compiling true
                        result undefined
                        sibilant-string action.sibilant-string })

         state))

